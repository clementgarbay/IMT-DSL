grammar fr.imta.clementdamien.dsl.selenium.MySelenium with org.eclipse.xtext.common.Terminals

generate mySelenium "http://www.imta.fr/clementdamien/dsl/selenium/MySelenium"



Program:
	Statements | Functions 
;

Functions:
	(functions += Function)+ (mainFunction = MainFunction)
;

Statements:
	statements += Statement+
;

MainFunction:
    'def' 'main' '{'
        statements = Statements
    '}'
;

Function:
    'def' name=FunctionName params = FunctionParameters? '{'
        statements = Statements
    '}'
;

FunctionName:
    name=ID
;

FunctionCall:
    ref=[FunctionName] (params = FunctionCallParameters)
;

Projection:
    selector=Selector '.' projectionAction=ProjectionAction
;

ProjectionAction: 'value' | 'text' | 'html';

Statement:
    Action |
    AssignAction | 
    FunctionCall | 
    AssertEquals | 
    AssertContains | 
    NavigationAction
;

AssertEquals:
    'assert' assertableElement += AssertableElement '=' assertableElement += AssertableElement
;

AssertContains:
    'assert' container = AssertableElement 'contains' element = AssertableElement
;

AssertableElement: 
    Variable | StringParameter | Projection | FunctionCall
;

NavigationAction:
    action=NavigationActionType param=STRING
;
NavigationActionType:
    "go" | "openBrowser"
;



Action:
    action=ActionType target=ActionTarget (param=ActionParameter)?
;

// assign action is specific action 
AssignAction:
	'assign' target=ActionTarget variable=Variable
; 

ActionTarget:
	Selector | VariableRef | FunctionCall
;

ActionParameter:
	Selector | ActionParameterString
;

ActionParameterString:
	value=STRING
;


Selector:
    element=DOMElement '[' (attrs = Attributes)? ']'  all?='.all'?
;

Attributes:
	attrs += Attribute (',' attrs += Attribute)* 
;

Attribute:
    name=DOMAttribute '=' (value = STRING | variable = VariableRef)
;


DOMAttribute: 'name' | 'text' | 'alt' | 'checked' | 'class' | 'id' | 'type';

DOMElement: 'link' | 'button' | 'checkbox' | 'input' | 'h1' | 'body' | 'select' | 'a' | 'img' | 'div';

ActionType: 'click' | 'fill' | 'choose' | 'count';

FunctionParameters:
    '(' variables += Variable (',' variables += Variable)* ')'
;

FunctionCallParameters:
    '(' ( variables += VariableCall (',' variables += VariableCall)* )? ')'
;

VariableCall:
    VariableRef | StringParameter
;

VariableRef:
    ref=[Variable]
;

Variable:
    name=ID
;

//terminal ID : '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
//terminal VAR_ID : ('a'..'z'|'A'..'Z'|'_')+;

StringParameter:
    value=STRING
;
